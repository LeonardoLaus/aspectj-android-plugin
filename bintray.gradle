apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'
def isJava = project.plugins.hasPlugin(JavaPlugin)
def artifactId = getArtifactId()

task sourcesJar(type: Jar) {
    if (isJava) {
        from sourceSets.main.java.srcDirs
    } else {
        from android.sourceSets.main.java.srcDirs
    }
    classifier = 'sources'
}
if (!isJava) {
    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

tasks.withType(Javadoc) {
    options {
        encoding 'UTF-8'
        charSet 'UTF-8'
        links "http://docs.oracle.com/javase/7/docs/api"
        if (!isJava) {
            linksOffline "https://developer.android.com/reference", "${android.sdkDirectory}/docs/reference"
        }
    }
}

def getArtifactId() {
    def selfConfig = project.hasProperty('artifactId')
    def artifactId = selfConfig ? project.artifactId : project.name
    println "project artifactId now is ${artifactId}. Use ext {artifactId = xxx} to customizing."
    return artifactId
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'

                name artifactId
                url config.versions.siteUrl

                licenses {
                    license {
                        name config.license.name
                        url config.license.url
                    }
                }

                developers {
                    developer {
                        id config.developer.id
                        name config.developer.name
                        email config.developer.email
                    }
                }
                scm {
                    connection config.versions.gitUrl
                    developerConnection config.versions.gitUrl
                    url config.versions.siteUrl
                }
            }
        }
    }
}
bintray {
    user = config.bintray.user
    key = config.bintray.apikey
    configurations = ['archives']

    override = true //[Default: false] Whether to override version artifacts already published

    pkg {
        repo = 'Maven'
        name = artifactId
        description = config.versions.desc
        websiteUrl = config.versions.siteUrl
        vcsUrl = config.versions.gitUrl
        issueTrackerUrl = config.versions.issueTrackerUrl
        licenses = ["Apache-2.0"]
        publicDownloadNumbers = true
        githubRepo = config.versions.githubRepo //Optional Github repository
        githubReleaseNotesFile = 'README.md' //Optional Github readme file

        dryRun = false //[Default: false] Whether to run this as dry-run, without deploying
        publish = true //[Default: false] Whether version should be auto published after an upload

        version {
            name = config.versions.version
            desc = "release ${this.version}"
            released = new Date()
            vcsTag = config.versions.version
        }
    }
}

println("project.name=${project.name}")